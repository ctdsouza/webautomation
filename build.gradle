group 'com.toppr.web'
version '1.0-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.5

repositories {
    maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
    compile group: 'org.seleniumhq.selenium', name: 'selenium-java', version:'2.48.2'
    compile group: 'com.opera', name: 'operadriver', version:'1.5'
    compile group: 'io.github.bonigarcia', name: 'webdrivermanager', version:'1.2.0'
    compile group: 'org.uncommons', name: 'reportng', version:'1.1.4'
    compile group: 'org.seleniumhq.selenium', name: 'selenium-remote-driver', version:'2.48.2'
    compile group: 'org.json', name: 'json', version:'20160212'
    testCompile group: 'org.testng', name: 'testng', version:'6.9.4'
    testCompile group: 'com.google.inject', name: 'guice', version:'3.0'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

tasks.withType(Test) {
    testLogging {
        // set options for log level LIFECYCLE
        events "passed", "skipped", "failed"
        showExceptions true
        exceptionFormat "short"
        showCauses true
        showStackTraces true

        // set options for log level DEBUG and INFO
        debug {
            events "started", "passed", "skipped", "failed", "standardError"
            exceptionFormat "full"
        }
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
    useTestNG {
        systemProperties = System.getProperties()
        suites 'src/test/java/com/toppr/web/Websitetestng.xml'
    }
}

task acceptancetest(type:Test) {
    useTestNG {
        systemProperties = System.getProperties()
        suites 'src/test/java/com/toppr/web/Acceptancetestng.xml'
    }
}

task productionsanity(type:Test) {
    useTestNG {
        systemProperties = System.getProperties()
        suites 'src/test/java/com/toppr/web/ProductionSanitytestng.xml'
    }
}